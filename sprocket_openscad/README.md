An OpenSCAD file that generates a bicycle sprocket


```openscad

// Number of teeth for the sprocket
// The angle that each tooth is formed at
// The height of each tooth above the center of the roller

teeth = 15;
tooth_angle = 30;
tooth_height = 1;

roller = 3.96875;  // Radius of the chain's "roller," a constant - for a bicycle chain, it is 5/16" divided by 2 ~= 3.97mm

// The rollers of a chain are located at what is, in essence,
// the vertices of a regular polygon. This function calculates
// the distance from the center of the polygon to one of those vertices.

function roller_radius_polygon(teeth, pitch = 12.7) =
    pitch / (2 * sin(180 / teeth));  // Divides 360 degrees by the number of teeth to calculate that radius

// Function to generate a range of angles by dividing 360 degrees by the number of teeth

function angle_range(teeth) = [0 : 360 / teeth : 360];  // Returns a range of angles from 0 to 360

angles = angle_range(teeth);  // Generates angles for the specified number of teeth

circumradius = roller_radius_polygon(teeth);  // Calculates the circumradius based on the specified number of teeth and default pitch

//projection(cut = true) { // Uncomment if you want to generate a 2D rendering that can be exported as an SVG or a DXF file
difference() {
    // Base circle representing the sprocket body, extruded to a height of 2mm
    color([0.8, 0.0, 0.0])  // Red color for the base circle
    linear_extrude(height = 2, center = true)
        circle(r = circumradius + tooth_height, $fn = 100);  // The base circle with a radius slightly larger than the circumradius

    // Loop to create features at each angle generated by angle_range
    for (angle = angles) {
        rotate(angle) {
            translate([circumradius, 0, 0]) {
                union() {
                    // First polygon extrusion, rotated 20 degrees clockwise
                    color([0.8, 0.0, 0.0])  // Red color for the polygon
                    linear_extrude(height = 3, center = true)
                        rotate([0, 0, tooth_angle])
                        polygon(points = [[0, 0], [0, -roller], [20, -roller], [20, roller], [0, roller]]);

                    // Second polygon extrusion, rotated 20 degrees counter-clockwise
                    color([0.8, 0.0, 0.0])  // Red color for the polygon
                    linear_extrude(height = 3, center = true)
                        rotate([0, 0, -tooth_angle])
                        polygon(points = [[0, 0], [0, -roller], [20, -roller], [20, roller], [0, roller]]);

                    // Central circular extrusion representing a roller at each angle
                    color([0.8, 0.0, 0.0])  // Red color for the circle
                    linear_extrude(height = 3, center = true)
                        circle(r = roller, $fn = 100);
                }
            }
        }
    }
}
//}


```
